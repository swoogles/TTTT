plugins {
  id 'org.unbroken-dome.test-sets' version '1.5.0'
  id "com.github.spotbugs" version "1.6.2"
  id "net.ltgt.errorprone" version "0.0.14"
  id 'com.github.ksoichiro.console.reporter' version '0.5.0'
  id 'org.springframework.boot' version '2.0.3.RELEASE'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {                                                                                                                                                            
  mavenCentral()
}    

dependencies {
    // https://mvnrepository.com/artifact/org.springframework/spring-beans
    compile group: 'org.springframework', name: 'spring-beans', version: '5.0.8.RELEASE'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.3.RELEASE'

    testCompile "org.testng:testng:6.14+"
    errorprone 'com.google.errorprone:error_prone_core:2.3.1'

    // This is all needed because of static bullshit.
    // https://mvnrepository.com/artifact/org.powermock/powermock-module-testng
    testCompile group: 'org.powermock', name: 'powermock-module-testng', version: '1.7.4'
    // https://mvnrepository.com/artifact/org.powermock/powermock-api-mockito2
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.4'
}

test {
  useTestNG()
}

testSets {
    integrationTest
}

integrationTest {
  useTestNG()
  // TODO Turn this into a reusable function that can be used for test & integrationTest
	testLogging {
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			}
		}
	}
}

test {
  // TODO Turn this into a reusable function that can be used for test & integrationTest
	testLogging {
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			}
		}
	}

  jacoco {
    append = false
      destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
      classDumpDir = file("$buildDir/jacoco/classpathdumps")
  }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        // It'd be nice to spit out this dir name whenever jacoco runs via gradle.
        html.destination file("${buildDir}/jacocoHtml")
    }
}

check {
  dependsOn integrationTest
  dependsOn jacocoTestReport
  dependsOn bootRun // Make sure Spring boot application can start
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled false
      html.enabled true
  }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.000
            }
        }

    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

// TODO Consider if this should be connected
// to run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification

springBoot {
    mainClassName = 'com.billding.spring.Application'
}
